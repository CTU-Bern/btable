{smcl}
{* *! version 1.0.0  26jan2021}{...}
{hline}
{cmd:help btable} {right:also see:  {help btable_format}}
{hline}

{title:Title}

{phang}
{bf:btable} {hline 2} Descriptive table in an external file


{marker syntax}{...}
{title:Syntax}

{p 4 6 2}
{cmdab:btable} {varlist} {ifin} {cmd:,} 
{opth sav:ing(filename)} 
[{opth by:(varlist:groupvar)} {it:options}]


{synoptset 35 tabbed}{...}
{synopthdr}
{synoptline}
{syntab:Output}
{p2coldent:* {opth sav:ing(filename)}}filename to store the table{p_end}

{syntab:Main}
{synopt: {opth by:(varname:groupvar)}}variable defining the groups, used to separate the table{p_end}
{synopt:{opth cat:egorical(varlist)}}specify categorical variables{p_end}
{synopt:{opth conti:nuous(varlist)}}specify continuous variables{p_end}
{synopt:{opth count(varlist)}}specify count variables{p_end}
{synopt:{opth tte(varlist)}}specify time-to-event variables{p_end}

{syntab:Options for categorical variables}
{synopt: {opt denom:inator(nonmiss)}}use non-missing values as denominator for categorical variables {p_end}
{synopt: {opt all:levels}}include empty levels of categorical variables{p_end}

{syntab:Options for count variables}
{synopt: {opth exp_time(varlist)}}exposure time for count variables {p_end}
{synopt: {opth exp_time_units(string)}}optional units for exposure time {p_end}
{synopt:{cmdab:nbreg}}use negative binomial regression to calculate incidence rate ratios {p_end}

{syntab:Options for time-to-event variables}
{synopt: {opth fail(varlist)}}failure indicator for time-to-event variables {p_end}
{synopt: {opth trunc_time(numlist)}}truncation time to calculate restricted mean survival time for time-to-event variables {p_end}
{synopt: {opth surv_time_units(string)}}optional units for time-to-event variables {p_end}
{synopt: {cmdab:rmst_model(}{it:{help btable##rmst_modelspec:rmst_modelspec}}{cmd:)}}model for calculation of restricted mean survival time, default is npar {p_end}
{synopt: {opth fpm_df(numlist)}}degree of freedom for flexible parametric survival models {p_end}

{syntab:Group comparison (specified by {opt by(groupvar)})}
{synopt: {opth order(numlist)}}order of the by groups{p_end}
{synopt:{cmdab:effect(}{it:{help btable##effectspec:effectspec}}{cmd:)}}effect measures to be calculated, default is {it:all} {p_end}
{synopt:{cmdab:test(}{it:{help btable##testspec:testspec}}{cmd:)}}tests to be calculated, default is {it: all}{p_end}

{syntab:Confidence intervals}
{synopt:{opt l:evel(#)}}set confidence level; default is {cmd:level(95)}{p_end}
{synopt:{cmdab:prci(}{it:{help btable##prcispec:prcispec}}{cmd:)}}confidence interval for proportions,
	default is {it:wilson} {p_end}
{synopt:{cmdab:rdci(}{it:{help btable##rdcispec:rdcispec}}{cmd:)}}confidence interval for risk differences,
	default is {it:wald} {p_end}
{synopt:{cmdab:rrci(}{it:{help btable##rrcispec:rrcispec}}{cmd:)}}confidence interval for risk ratios,
	default is {it:katz} {p_end}
{synopt:{cmdab:orci(}{it:{help btable##orcispec:orcispec}}{cmd:)}}confidence interval for odds ratios,
	default is {it:woolf} {p_end}
{synopt:{cmdab:hlmdci(}{it:{help btable##hlmdcispec:hlmdcispec}}{cmd:)}}confidence interval for Hodges-Lehmann median differences,
	default is {it:newson} {p_end}
{synopt:{cmdab:mwsci(}{it:{help btable##mwscispec:mwscispec}}{cmd:)}}confidence interval for 
	the Mann-Whitney statistic, default is {it:fay} {p_end}
{synopt:{cmdab:irrci(}{it:{help btable##irrcispec:irrcispec}}{cmd:)}}confidence interval for 
	incidence ratio ratio, default is {it:exact} {p_end}
	
{syntab:Options for tests}
{synopt:{cmdab:ttest(}{it:{help btable##ttest_options:ttest_options}}{cmd:)}}options to be passed to {manhelp ttest R:ttest} {p_end}
{synopt:{cmdab:qreg(}{it:{help btable##qreg_options:qreg_options}}{cmd:)}}options to be passed to {manhelp qreg R:qreg} {p_end}
{synopt:{cmdab:lr(}{it:{help btable##lrspec:lrspec}}{cmd:)}}use likelihood ratio tests for chi2, poisson, nbreg or cox {p_end}
{synopt:{cmdab:ranksum_exact}}report exact p-value for rank-sum test; by default, exact p-value is computed when total sample size < 200 {p_end}
{synopt:{cmdab:poisson_test(}{it:{help btable##poisson_testspec:poisson_testspec}}{cmd:)}}Alternative Poisson
	tests for count variables, default is {it:midp}{p_end}
	
{syntab:User-defined commands}
{synopt:{cmdab:user_command(}{it:{help btable##user_commandspec:user_commandspec}}{cmd:)}}user-defined command{p_end}
{synopt:{cmdab:user_var(}{it:{help btable##user_varspec:user_varspec}}{cmd:)}}variables to apply user-defined command{p_end}
{synopt:{cmdab:user_post(}{it:{help btable##user_postspec:user_postspec}}{cmd:)}}return results from 
	user-defined command{p_end}
{synopt:{cmdab:user_name(}{it:{help btable##user_namespec:user_namespec}}{cmd:)}}names for variables 
	generated by user-defined commands{p_end}
{synopt: {opt user_parse("pchar")}}parsing character to separate user_command, user_var, user_post and user_name , default is "|" {p_end}

{syntab:Hierarchical data structure}
{synopt: {opth kid(varlist)}}specify kid variables{p_end}
{synopt: {opth parent(varlist)}}specify parent variables{p_end}
{synopt: {opth parent_level(numlist)}}specify levels of parent variables for which kids are defined, default is 1. {p_end}
{synoptline}
{p2colreset}{...}
{p 4 6 2}* {opt saving(filename)} is required.{p_end}

{p2colreset}{...}
{p 4 6 2}
See {manhelp btable_format R:btable_format} for formatting the table after generation.


{marker description}{...}
{title:Description}

{pstd}
{cmd:btable} generates a descriptive table with unformatted data for the specified variables. The table includes 
	descriptives for the total (with suffix _t) and may include descriptives for each group (with suffix _1, _2, ...), 
	measures for the difference between groups and p-values if {opt by(groupvar)} is specified.

{pstd}
Descriptives include 
number of events (stored in variables starting with nlev),
	proportion of events (pr) and confidence interval of the proportion (prlci and pruci) for categorical variables, 
mean (mean), median (p50), standard deviation (sd), quartiles (p25 and p75), interquartile range (iqr),
	minimum (min), maximum (max), range (range), sum (sum) 
		and confidence interval of the mean (meanlci and meanuci) for continuous variables,
number of events (nevents), exposure time (etime), incidence rate (ir) 
	and confidence intervals of the incidence rate (irlci and iruci) for count variables, 
and number of failures (nfails) and survival time (stime), median survival time (st50), 
	quartiles of the survival time (st25 and st75), confidence interval of the survival time (st50lci and st50uci),
	restricted mean survival time (rmst) and confidence interval of the restricted mean survival time (rmstlci and rmstuci)
	for time-to-event variables.

{pstd}
Measures for the difference are always accompanied by confidence intervals and include 
risk difference (rd, rd_lci and rd_uci), risk ratio (rr, rr_lci and rr_uci) and odds ratio (or, or_lci and or_uci) for categorical variables, 
mean difference (meand, meand_lci and meand_uci), median difference (medd, medd_lci, medd_uci), 
	Hodges-Lehmann median difference (hlmd, hlmd_lci and hlmd_uci)
	and the Mann-Whitney statistic (in the standardized form, corresponds to the area under the ROC-curve)(mws, mws_lci and mws_uci)
	for continuous variables,
incidence rate ratio (irr, irr_lci and irr_uci) and incidence rate difference (ird, ird_lciand ird_uci) for count variables, and
hazard ratio (hr, hr_lci and hr_uci) and restricted mean survival time difference (rmstd, rmstd_lci and rmstd_uci) for time-to-event variables.

{pstd}
P-values are from chi-squared (p_chi2) and Fisher's exact tests (p_fisher) for categorical variables,
either from Student's t-tests (p_ttest), Wilcoxon rank sum tests (p_ranksum) or quantile regression (p_qreg) 
	(if {it:groupvar} has two levels) or 
	from ANOVA (p_anova) and Kruskal-Wallis test (p_kwallis) if {it:groupvar} has more than two levels) for continuous variables,
from a Poisson test (p_poisson, either midp, exact or from a generalized linear model) 
	and optionally negative binomial regression (p_nbreg) for count variables, and
from a log-rank test (p_logrank), a Cox model (p_cox, either based on a Wald or likelihhod ratio test) or a 
	Wald test from the restricted mean survival time difference (p_rmstd, only if {it:groupvar} has two levels)
	for time-to-event variables.

{pstd}
The tables containing the raw data is stored in an external file "{it:filename}.dta" specified by {cmd:saving()}. If 
{opt by()} is used an ancilliary file "{it:filename}_group.dta" is generated. The table
can be formatted using {manhelp btable_format R:btable_format} using "{it:filename}.dta" as an input.


{marker options}{...}
{title:Options}

{dlgtab:Output}

{phang}
{opth sav:ing(filename)} specifies the {it:filename} to store the raw table. Can
be used as input for {manhelp btable_format R:btable_format}.{p_end}
 
{dlgtab:Main}

{phang}
{opth by:(varname:varname)} specifies the groupvar that defines the two groups that btable will compare. Not
specifiying groupvar implies that there is only one group. The labels of {it:groupvar} will be stored in a ancillary 
file {cmd:{it:filname}_group.dta}.

{phang}
{opth cat:egorical(varlist:varlist)} specifies that the variables in {it:varlist} are categorical. By
default a variable is assumed to be categorical if there are five or less distinct values. {p_end}

{phang}
{opth conti:nuous(varlist:varlist)} specifies that the variables in {it:varlist} are continuous. By
default a variable is assumed to be continuous if there are more than five distinct values. {p_end}

{phang}
{opth count(varlist)} specifies that the variables in {it:varlist} are counts.
	For each count variable an exposure time variable has to be given via {cmd:exp_time}. {p_end}

{phang}
{opth tte(varlist)} specifies that the variables in {it:varlist} are survival times, i.e. a time to an event
	indicated by {cmd:fail}.
	For each tte variable failure indicator has to be given via {cmd:fail}. {p_end} 

{dlgtab:Options for categorical variables}

{phang}
{opt denom:inator(nonmiss)} specifies that the number of non-missing values is used as denominator 
to calculate proportion and effect measure for categorical variables.
The default is to use the total number of values. {p_end}
	
{phang}
{opt all:levels} specifies that all levels should be shown for categorical variables. The default is to show only
non-empty levels.{p_end}

{dlgtab:Options for count variables}

{phang}
{opth exp_time(varlist)} defines the variables containing the exposure time for the count variables. 
	The length of {it:varlist} has to be either one (the same exp_time is then assumed for all count variables)
	or to correspond to the number of count variables.{p_end}

{phang}
{opth exp_time_units(string)} optional string that defines the units for the exposure time. 
The length of the string has to be one (assuming the same unit for all count variables) 
	or correspond the number of count variables.
If given, the output raw table contains a further variable with the units of the exposure time (etime_unit), 
	which is used for headers by {manhelp btable_format R:btable_format}. {p_end}

{phang}
{opt nbreg} calculates incidence rate ratio and its confidence interval from a negative binomial regression, 
	and adds the corresponding p-value from a Wald test (if test is not none).
	If not specified the ratio of the incidence rates and confidence intervals specified by
	{cmd:irci} (exact Poisson by default) are used and the p-value is not included.
	Note that {cmd:irci} does not have an effect of {cmd:nbreg} is specified.

	
{dlgtab:Options for time-to-event variables}
	
{phang}
{opth fail(varlist)} defines the failure indicator for the tte variables.
	Has to be a binary variable with 0 (or missing) meaning censored and 1 representing failure. 
	The length of {it:varlist} has to correspond to the number of tte variables.{p_end}

{phang}
{opth trunc_time(numlist)} specifies truncation time for each tte variable at which the restricted mean survival time 
	is calculated. 
	Has to be a {it:numlist} with a length of one (the same truncation time is then assumed for all tte variables)
	or matching to the number of tte variables specified by {opt tte(varlist)}. 
	If not given, the minimum of the largest observed event time over all groups 
	is used to calculate mean restricted survival time.{p_end} 
	
{phang}
{opth surv_time_units(string)} optional string that defines the units for the tte variables. 
The length of the string has to be one (assuming the same unit for all tte variables) 
	or correspond the number of tte variables.
If given, the output raw table contains a further variable with the units of the survival time (stime_unit), 
	which is used for headers by {manhelp btable_format R:btable_format}. {p_end}

{phang}{marker rmst_modelspec}{...}	
{opt rmst_model(rmst_modelspec)} specifies the model used to calculate the restricted mean survival time.
	Must only contain {it:npar} (non-parametric) or {it:fpm} (flexible parametric survival model).
	Must have a length of one (the same model is then assumed for all tte variables)
	or matching to the number of tte variables specified by {opt tte(varlist)}. 
	The default is {opt rmst_model(npar)}.{p_end} 
	
{phang}
{opth fpm_df(numlist)} specifies the degress of freedom for flexible parametric survival models 
	used to calculate the restricted mean survival time. 
	Has to be a {it:numlist} with a length of one (the same df is then assumed for all models)
	or matching to the number of tte variables where {cmd:rmst_model} is {it:fpm}. 
	The default is {opt fpm_df(3)}. Does not have an effect if {cmd:rmst_model} is not {it:fpm}.{p_end} 
		
{dlgtab:Group comparison}

{pstd}
These options are only valid if {opth by:(varlist:groupvar)} is defined. By default all available effect measures and
tests are calculated (if the corresponding variable type is present) but this can be slow. 
Effect measures and tests that are not calculated are not available in {manhelp btable_format R:btable_format}.

{phang}
{opth order(numlist)} specifies the order of the groups defined by {opth by:(varlist:groupvar)} 
	in the table and, if there are two groups, the direction of the effect. 
	The {it:numlist} has to contain all the levels of the {it:groupvar}. The columns in the table are ordered 
	accordingly. The effect is presented as first level vs second level. {p_end}

{phang}{marker effectspec}{...}
{opt effect(effectspec)} specifies the effect measures that should be calculated. 
	Possible values are:

{phang2}	
	- {it: all} (the default) and {it:none}
	
{phang2}
	- for categorical variables:	
	{it:rd} (risk difference), {it:rr} (risk ratio) and {it:or} (odds ratio)

{phang2}
	- for continuous variables:
	{it:meand} (mean difference), {it:medd} (median difference), 
	{it:hlmd} (Hodges-Lehmann median difference) and {it:mws} (Mann-Whitney statistic)
	
{phang2} 
	- for count variables:	
	{it:irr} (incidence rate ratio) and {it:ird} (incidence rate difference)
	
{phang2} 
	- for time-to-event variables:	
	{it:hr} (hazard ratio) and {it:rmstd} (restricted mean survival time difference)
	
{phang}{marker testspec}{...}
{opt test(testspec)} specifies the tests to be calculated. 
	Possible values are:

{phang2}	
	- {it:all} (the default) and {it:none}
	
{phang2}
	- for categorical variables: {it:fisher} (Fisher's exact test) and {it:chi2} (chi-squared test)
	
{phang2} 
	- for continuous variables:
	{it:ttest} (Student's t-test), 
	{it:ranksum} (Wilcoxon rank-sum test), 
	{it:qreg} (Wald test for {it:groupvar} in a median regression),
	and - if {it:groupvar} has more than two levels - 
	{it:anova} (ANOVA for continuous variables) and
	{it:kwallis} (Kruskal-Wallis test for continuous variables)
	
{phang2} 
	- for count variables: {it:poisson} (Poisson exact test)
	
{phang2} 
	- for time-to-event variables: {it:logrank} (log-rank test), 
	{it:cox} (Wald test or likelihhod ratio test (if {cmd:cox_lr} is used) from a Cox model) and 
	{it:rmstd} (Wald test of restricted mean survival time difference)

	
{dlgtab:Confidence intervals}

{pstd}
Options for confidence intervals of effect measures do not have an effect 
if {opth by:(varlist:groupvar)} is not defined or
if the corresponding effect measure is not included in {cmdab:effect(}{it:{help btable##effectspec:effectspec}}{cmd:)}.

{phang}
{opt level(#)} specifies the confidence level for confidence
   intervals, as a percentage.  The default is {opt level(95)} or as set by {helpb set level}.

{phang}{marker prcispec}{...}
{opt prci(prcispec)} specifies the type of confidence interval for proportions ({help btable##Brown2001:Brown, 2001}).
	Possible options are 
	{it:wilson} (Wilson score confidence interval, {help btable##Wilson1927:Wilson, 1927}, the default), 
	{it:wald} (asymptotic Wald-type confidence inerval), 
	{it:exact} (exact binomial confidence intervals, {help btable##Clopper1934: Clopper-Pearson, 1934}), 
	{it:agresti} (Agresti-Coull confidence intervals {help btable##Agresti1998: Agresti and Coull, 1998}) and 
	{it:jeffreys} (Jeffreys confidence intervals). {p_end}
 
{phang}{marker rdcispec}{...}
{opt rdci(rdcispec)} specifies the type of confidence interval for risk difference. Possible options are 
	{it:wald} (asymptotic Wald-type confidence inerval, the default), 
	{it:ac} ({help btable##Agresti2000:Agresti and Caffo, 2000}), 
	{it:nhs} (method 10 from {help btable##Newcombe1998:Newcombe, 1998}, 
		based on Wilson's score method, {help btable##Wilson1927:Wilson, 1927}), 
	{it:wall} ({help btable##Wallenstein1997:Wallenstein, 1997}) and 
	{it:mn} ({help btable##Miettinen1995:Miettinen and Nurminen, 1995}). {p_end}

{phang}{marker rrcispec}{...}		
{opt rrci(rrcispec)} specifies the type of confidence interval for risk ratio. Possible options are
	{it:katz} ({help btable#Katz1978:Katz, 1978}, the default) and 
	{it:koopman} ({help btable##Koopman1984:Koopman, 1984}). {p_end}

{phang}{marker orcispec}{...}	
{opt orci(orcispec)} specifies the type of confidence interval for odds ratio. Possible options are
	{it: woolf} ({help btable##Woolf1955:Woolf, 1955}, the default),
	{it: exact} (exact interval from {help btable##Cornfield1956:Cornfield, 1956}),
	{it: cornfield} (approximate interval from {help btable##Cornfield1956:Cornfield, 1956}),
	{it: agresti} (endpoint-adjusted, independence smoothed confidence interval recommended 
		by {help btable##Agresti1999:Agresti, 1999}) and
	{it: gart} ({help btable##Gart1966:Gart, 1966}). {p_end}
	
{phang}{marker hlmdcispec}{...}
{opt hlmdci(hlmdcispec)} specifies the type of confidence interval for Hodges-Lehmann median difference. 
	Possible options are
	{it:newson} ({help btable##Newson2006a:Newson, 2006a}, the default) and 
	{it:lehmann} (method from {help btable##Lehmann1963:Lehmann, 1963}, 
		implemented by {help btable##Wang2000:Wang, 2000}) . {p_end}

{phang}{marker mwscispec}{...}	
{opt mwsci(mwscispec)} specifies the type of confidence interval for the Mann-Whitney statistic. Possible options are 
	{it:fay} (compatible asymptotic confidence interval from {help btable##Fay2018:Fay and Malinovsky, 2018}, the default),
	{it:delong} ({help btable##Delong1988:DeLong, DeLong, and Clarke-Pearson, 1988}),
	{it:jackknife} ({help btable##Newson2006b:Newson, 2006b}),
	{it:bamber} ({help btable##Bamber1975:Bamber, 1975}) and 
	{it:hanley} ({help btable##Hanley1982:Hanley and McNeil, 1982}). {p_end}

{phang}{marker irrcispec}{...}	
{opt irrci(irrcispec)} specifies the type of confidence interval for the incidence rate ratio. Possible options are 
	{it:exact} (exact confidence interval used by {manhelp epitab R:epitab}, the default),
	{it:oim} (based on standard errors using the observed information matrix from a Poisson regression) and
	{it:robust} (Huber/White/sandwich estimator of the variance ({help btable##Huber1967:Huber, 1967} and 
	White, {help btable##White1980:1980}, {help btable##White1982:1982}). {p_end}
	
{phang}
Further confidence intervals are calculated in the following way:

{phang2}
- mean and mean difference: Wald-type confidence intervals are calculated using {manhelp ci R:ci means} 
	and {manhelp ttest R:ttest}. 
	
{phang2}	
- median difference: Wald-type confidence intervals are calculated based on the large sample 
	variance-covariance matrix (e.g. {help btable##Koenker2005:Koenker (2005, 73)}) using {manhelp qreg R:qreg}.

{phang2}
- incidence rate: exact Poisson confidence intervals are calculated (using {manhelp ci R:ci means, poisson}).

{phang2}
- incidence rate difference: Wald-type confidence intervals are calculated (using {manhelp ir R:ir}).

{phang2}
- median survival time: confidence intervals are based on a log-log-transformation of the survival time 
	(using {manhelp stci R:stci}).
	
{phang2}	
- hazard ratio: Wald-type confidence intervals are calculated from a Cox regression (using {manhelp stcox R:stcox}).

{phang2}
- restricted mean survivial time and difference: Wald-type confidence intervals are calculated with
	standard errors described in {help btable##Collett2015:Collett (2015, 390)}

	
{dlgtab:Options for tests}

{phang}{marker ttest_options}{...}	
{opt ttest(ttest_options)} specifies options to be passed to {manhelp ttest R:ttest}.  {p_end}

{phang}{marker qreg_options}{...}
{opt qreg(qreg_options)} specifies options to be passed to {manhelp qreg R:qreg}.  {p_end}
	
{phang}{marker lrspec}{...}	
{opt lr(lrspec)} calculates likelihood ratio chi-squared instead of Pearson's chi-squared test 
	({help btable##Fienberg1980:Fienberg, 1980}) and likelihood ratio tests instead of Wald-tests 
		for Poisson regression (if {cmd:poisson_test} is {it:oim}), negative binomial regression (if {cmd:nbreg} is specified) 
		or Cox-regression. Options are chi2, poisson, nbreg or cox. {p_end}
	
{phang}
{opt ranksum_exact} calculates an exact p-value for the Wilcoxon rank-sum test for large sample sizes (>200). 
	By default, an exact p-value is only computed when total sample size {ul:<} 200. {p_end}

{phang}{marker poisson_testspec}{...}	
{opt poisson_test(poisson_testspec)} specifies alternative Poisson tests for count variables. Possible options are 
	{it:midp} (mid-p adjusted p-values used by {manhelp epitab R:epitab}, the default),
	{it:exact} (exact p-values, more conservative than the mid-p-adjusted p-values),
	{it:oim} (based on standard errors using the observed information matrix from a Poisson regression) and
	{it:robust} (Huber/White/sandwich estimator of the variance ({help btable##Huber1967:Huber, 1967} and 
	White, {help btable##White1980:1980}, {help btable##White1982:1982}). {p_end}	
	
{dlgtab:User-defined commands}

{phang}{marker user_commandspec}{...}
{opt user_command(user_commandspec)} specifies a command applied to the dataset. Typically, the command  includes 

{phang2}	
keyword {it:variable} that refers to the variable names or categories and will be replaced by variables 
	specified in {cmd:user_var}, and
	
{phang2}	
keyword {it:by} that refers to the {it:groupvar} specified by the {cmd:by} option. 

{phang2}
If {cmd:user_post} is not defined, e(b), e(V) and e(df_r) (if available)
	are used to construct point estimate, confidence interval and p-value in variables 
	user_pe, user_lci, user_uci and user_p, respectively.
	Alternatively, any value stored by {cmd:user_command} can be used via {cmd: user_post}. The name 
	of the variables in the raw data table saved in {it:filename} can be modified by {cmd:user_name}.
	
{phang2}	
More than one user-defined command can be specified, separated by a character specified in {cmd:user_parse} ("|" by default).

{phang}{marker user_varspec}{...}
{opt user_var(user_varspec)} specifies the variable category or variable name on which 
	the command given by {cmd:user_command} is applied to. 
	{it: user_varspec} may include {cmd:conti}, {cmd:cat}, {cmd:count}, {cmd:tte} or {it:varlist}.
	If more than one user-defined command is specified (i.e. if {it: user_commandspec}
	contains the parsing character defined by {cmd:user_parse}), 
	the same number of variable sets have to be specified in {it:user_varspec}, separated by the same 
	parsing character.

{phang}{marker user_postspec}{...}
{opt user_post(user_postspec)} specifies where results from {cmd: user_command} are stored. 
	Each entry in {it:user_postspec} will be used to generate a variable in the raw table stored in {it:filename}.
	Variables will be named according to {cmd:user_name} or number sequentially with stub "user" (if {cmd:user_name}
	is not given).
	If {cmd:user_postspec} is not given, e(b), e(V) and e(df_r) (if available) are used to construct 
	point estimate, confidence interval and p-value 
	with variable names user_pe, user_lci, user_uci and user_p, respectively. 
	If more than one user-defined command is specified, {it:user_postspec} can refer to different
	stored results for each command (separated by the parsing character given by {cmd: user_parse}). 
	If no parsing character is used, the same storage location is assumed for all commands.

{phang}{marker user_namespec}{...}
{opt user_name(user_namespec)} specifies a list of variable names for the variables generated by the 
	user-defined command. Length has to correspond to {cmd:user_post} or to four if {cmd:user_post} is not spceified
	(corresponding to point estimate, lower confidence limit, upper confidence limit and p-values). 
	If not given the default names are used, i.e. 
	user_pe, user_lci, user_uci and user_p (if {cmd:user_post} is not given) or 
	user1, user2, ... (if {cmd:user_post} is given).
	
	{phang2}
	If more than one user-defined command is specified, more than one list of names may be 
	given, separated by the parsing character given by {cmd: user_parse}. 
	If no parsing character is used, the same name is assumed for all commands (results may be
	overwritten if two commands are applied to the same variables, a warning is issued).
	Default names are user{it:i}_pe, user{it:i}_lci, user{it:i}_lci, user{it:i}_p if {cmd:user_post} is not given 
	and to user{it:i}1 user{it:i}2, ... if {cmd:user_post} is given with {it:i} being the number of the user defined 
	command.
	
	{phang2}	
	If the user-defined command includes a measure specific to {it:groupvar} specified by {cmd:by} (i.e. a descriptive),
	the names for the group-specific results should be specified via stub{it:_1}, stub{it:_2}, ... 
	with the number corresponding to the order of the labels in {it:groupvar} (not the label itself).
	The stub can then be used in {cmd:ft_type} in {manhelp btable_format R:btable_format}.

{phang}{opt user_parse("pchar")} specifies the parsing character used to separate {cmd:user_command}, 
	{cmd:user_var}, {cmd:user_post} and {cmd:user_name} if more than one user defined command is specified. 
	The default is "|". The same number of parsing characters has to be given in user_command and user_var, 
	user_post and user_name may include no parsing character or the same number as user_command and user_var.
	If they do not contain parsing characters, the same values are assumed for all user defined commands.
	{p_end}	
		
{dlgtab:Hierarchical data structure}

{phang}
{opth kid(varlist)} specifies a list of kid variables in a hierarchical data structure, i.e. 
	variables which are only defined if another variable---the parent variable---takes a specific value.
	
{phang}	
{opth parent(varlist)} specifies parent variables for hierarchical data structure. 
	Length of {it:varlist} has to correspond to {cmd:kid}({it:varlist}), i.e. for each kid a parent has to be specified.
	
{phang}	
{opth parent_level(numlist)} optional specification of the level of the parent variable for which the kid is defined.
	Only one level can be given for each parent/kid pair.
	Length of {it:numlist} has to be one (assuming the same level for all parent/kid pairs) 
		or correspond to {cmd:kid}({it:varlist}). Default is 1.

	
{marker examples}{...}
{title:Examples}

{pstd}Data{p_end}
{phang}{cmd:. sysuse auto2}{p_end}

{pstd}Generate table {p_end}
{phang}{cmd:. btable price mpg rep78 headroom, saving("excars1")}{p_end}

{pstd}Use by option {p_end}
{phang}{cmd:. btable price mpg rep78 headroom, by(foreign) saving("excars2") denom(nonmiss)}{p_end}

{pstd}Non-missing values as denominator {p_end}
{phang}{cmd:. btable price mpg rep78 headroom, by(foreign) saving("excars3") denom(nonmiss)}{p_end}

{pstd}Force continous and categorical variables {p_end}
{phang}{cmd:. btable price mpg rep78 headroom, by(foreign) saving("excars4") cat(headroom) conti(rep78)} {p_end}

{pstd}Confidence interval for risk difference suggested by Agresti and Caffo. {p_end}
{phang}{cmd:. btable price mpg rep78 headroom, by(foreign) saving("excars5") rdci(ac)}{p_end}

{pstd}Limited effect measures and tests {p_end}
{phang}{cmd:. btable price mpg rep78 headroom, by(foreign) saving("excars6") effect(rd meand) test(fisher ttest)}{p_end}

{pstd}Time-to-event-variable {p_end}
{phang}{cmd:. set seed 1} {p_end}
{phang}{cmd:. gen tta = rexponential(20)} {p_end}
{phang}{cmd:. gen discharge = rbinomial(1,0.8)} {p_end}
{phang}{cmd:. label var tta "Time operational"} {p_end}
{phang}{cmd:. btable price mpg rep78 headroom tta, by(foreign) saving("excars7") tte(tta) fail(discharge) surv_time_units(years) trunc_time(10)}{p_end}

{pstd}User-defined commands with e(b) and e(V) {p_end}
{phang}{cmd:. btable price mpg rep78 headroom, by(foreign) saving("excars8") user_command(regress variable by, vce(robust) user_var(conti)}{p_end}

{pstd}User-defined commands with user_post {p_end}
{phang}{cmd:. cap program drop uest}{p_end}
{phang}{cmd:. program uest, rclass}{p_end}
{phang}{cmd:. syntax varlist}{p_end}
{phang}{cmd:. tokenize `varlist'}{p_end}
{phang}{cmd:. regress `1' `2', vce(robust)}{p_end}
{phang}{cmd:. matrix A = r(table)}{p_end}
{phang}{cmd:. local a1 = A[1,1]}{p_end}
{phang}{cmd:. local a2 = A[5,1]}{p_end}
{phang}{cmd:. local a3 = A[6,1]}{p_end}
{phang}{cmd:. local a4 = A[4,1]}{p_end}
{phang}{cmd:. return scalar pe = `a1'}{p_end}
{phang}{cmd:. return scalar lci = `a2'}{p_end}
{phang}{cmd:. return scalar uci = `a3'}{p_end}
{phang}{cmd:. return scalar p = `a4'}{p_end}
{phang}{cmd:. ereturn clear}{p_end}
{phang}{cmd:. end}{p_end}
{phang}{cmd:. btable price mpg rep78 headroom, by(foreign) saving("excars9") user_var(price) user_command(uest variable by) user_post(r(pe) r(lci) r(uci) r(p)) user_name(user1 user2 user3 user4)}{p_end}


{pstd}Check results {p_end}
{phang}{cmd:. use "excars1", clear} {p_end}

{pstd}Format table {p_end}
{phang}{cmd:. btable_format using "excars1", clear} {p_end}

{marker results}{...}
{title:Stored results}

{phang} {cmd:btable} stores a dta-file "{it:filename}.dta" specified in {cmd:saving()}. In addtion, if the {cmd:by()} option is used, an ancilliary file
	called "{it:filename}_group.dta" is generated. {p_end}


{marker references}{...}
{title:References}

{marker Agresti1998}{...}
{phang}
Agresti, A. & Coull, B. A. 1998.
Approximate is better than “exact” for interval estimation of binomial proportions.
{it:The American Statistician} 52: 119-126.

{marker Agresti1999}{...}
{phang}
Agresti, A. 1999.  
On logit confidence intervals for the odds ratio with small samples.
{it:Biometrics} 55: 597-602.

{marker Agresti2000}{...}
{phang}
Agresti A. and Caffo B. 2000. 
Simple and effective confidence intervals for proportions and differences 
of proportions result from adding two successes and two failures. 
{it:The American Statistician} 54: 280-288. 

{marker Bamber1975}{...}
{phang}
Bamber, D. 1975. The area above the ordinal dominance graph and the area below
the receiver operating characteristic graph.
{it:Journal of Mathematical Psychology} 12: 387-415.

{marker Brown2001}{...}
{phang}
Brown, L. D., T. T. Cai, and A. DasGupta. 2001.  
Interval estimation for a binomial proportion.  
{it:Statistical Science} 16: 101-133.

{marker Clopper1934}{...}
{phang}
Clopper, C. J., and Pearson, E. S. 1934.  
The use of confidence or fiducial limits illustrated in the case of the binomial.
{it: Biometrika} 26: 404-413.

{marker Collett2015}{...}
{phang}	
Collett, D. 2015. 
{it: Modelling Survival Data in Medical Research.} 3rd ed. Boca Raton, Fl: Chapman & Hall/CRC

	
{marker Cornfield1956}{...}
{phang}
Cornfield, J. 1956. 
A statistical problem arising from retrospective studies.
In Vol. 4 of {it:Proceedings of the Third Berkeley Symposium}, ed.
J. Neyman, 135-148. Berkeley, CA: University of California Press.

{marker Delong1988}{...}
{phang}
DeLong, E. R., D. M. DeLong, and D. L. Clarke-Pearson. 1988. 
Comparing the areas under two or more correlated receiver operating characteristic curves: A nonparametric approach. 
{it:Biometrics} 44: 837-845.

{marker Fay2018}{...}
{phang}
Fay, M. P. and Malinovsky, Y. 2018. 
Confidence intervals of the Mann-Whitney parameter that are compatible with the Wilcoxon-Mann-Whitney test. 
{it:Statistics in medicine} 1-16.

{marker Fienberg1980}{...}
{phang}
Fienberg, S. E. 1980. 
{it: The Analysis of Cross-Classified Categorical Data.} 
2nd ed. Cambridge, MA: MIT Press

{marker Gart1966}{...}
{phang}
Gart, J. J. 1966. 
Alternative analyses of contingency tables. 
{it:Journal of the Royal Statistical Society} Series B 28: 164-179.

{marker Hanley1982}{...}
{phang}
Hanley, J. A., and McNeil, B. J. 1982.  
The meaning and use of the area under a receiver operating characteristic (ROC) curve. 
{it:Radiology} 143: 29-36.

{marker Huber1967}{...}
{phang}
The behavior of maximum likelihood estimates under nonstandard conditions. In Vol. 1 of 
{it:Proceedings of the Fifth Berkeley Symposium on Mathematical Statistics and Probability},
221–233. Berkeley: University of California Press.

{marker Katz1978}{...}
{phang}
Katz, D., Baptista, J., Azen, S. P. and Pike, M. C. 1978. 
Obtaining confidence intervals for the risk ratio in cohort studies. 
{it:Biometrics} 34: 469-474.

{marker Koenker2005}{...}
{phang}
Koenker, R. 2005. {it:Quantile Regression.} New York: Cambridge University Press.


{marker Koopman1984}{...}
{phang}
Koopman, P. 1984. 
Confidence intervals for the ratio of two binomial proportions. 
{it:Biometrics} 40: 513-517

{marker Lehmann1963}{...}
{phang}
Lehmann, E. 1963. 
Nonparametric confidence intervals for a shift parameter.
{it:The Annals of Mathematical Statistics} 34: 1507-1512.

{marker Miettinen1995}{...}
{phang}
O. Miettinen and M. Nurminen. 1985.
Comparative analysis of two rates. 
{it:Statistics in Medicine} 4: 213-226.

{marker Newcombe1998}{...}
{phang}
R. G. Newcombe. 1998. 
Interval estimation for the difference between independent proportions: comparison of eleven methods.
{it:Statistics in Medicine} 17: 873-890.

{marker Newson2006a}{...}
{phang}
Newson, R.  2006a.
Confidence intervals for rank statistics: Percentile slopes, differences, and ratios.
{it:Stata Journal} 6: 497-520.

{marker Newson2006b}{...}
{phang}
Newson, R.  2006b.
Efficient calculation of jackknife confidence intervals for rank statistics.  
{it: Journal of Statistical Software} 15: 1-10.

{marker Wallenstein1997}{...}
{phang}
S. Wallenstein. 1997. 
A non-iterative accurate asymptotic confidence interval for the difference between two proportions.
{it:Statistics in Medicine} 16: 1329-1336.

{marker Wang2000}{...}
{phang}
Wang, D. 2000. Hodges-Lehmann estimation of a shift in location between two populations.
{it: Stata Technical Bulletin}. 52: 52–53.

{marker White1980}{...}
{phang}
White, H. L., Jr. 1980. 
A heteroskedasticity-consistent covariance matrix estimator and a direct test for heteroskedasticity.
{it:Econometrica} 48: 817–838.

{marker White1982}{...}
{phang}
White, H. L., Jr. 1982. 
Maximum likelihood estimation of misspecified models.
{it:Econometrica} 50: 1–25.

{marker Wilson1927}{...}
{phang}
Wilson, E. B. 1927.
Probable inference, the law of succession, and statistical inference. 
{it:Journal of the American Statistical Association} 22: 209-212.

{marker Woolf1955}{...}
{phang}
Woolf, B. 1955. 
On estimating the relation between blood group disease.
{it:Annals of Human Genetics} 19: 251-253.
Reprinted in
{it:Evolution of Epidemiologic Ideas: Annotated Readings on Concepts and Methods},
ed. S. Greenland, pp. 108-110. Newton Lower Falls, MA: Epidemiology Resources.







